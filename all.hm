/////////////////////////////////
// Everything we support today //
/////////////////////////////////

// You can define procedures and types.
// Definition order doesn't matter.
proc do_nothing() -> void { ret void; }
proc add(a: i32, b: i32) -> i32 { ret a + b; }
proc get_entity_pos(e: Entity) -> Vector2 { ret e.pos; }

type Vector2 { x: float32; y: float32; }
type Entity { id: i32; pos: Vector2; }


proc with_nested() -> i32 {
    // Procedure body defines a scope of it's own, so you can
    // define local (nested) procedures and types.
    proc two() -> i32 { ret one() + 1; }
    proc one() -> i32 { ret 1; }

    // Procedures and types of outer scopes are still available
    // if not shadowed local definition.
    type Vector2 { x: i32; y: i32; }
    proc more_nested() -> i32 {
        proc one() -> i32 { ret 10; }
        ret one();
    }

    proc get_y(v: Vector2) -> i32 { ret v.y; }

    ret more_nested();
}


proc stuff(a: float32, r: float32, v: Vector2) -> Vector2 {
    // Procedure body may contain statements

    // 1) Variable declarations...
    x : i32;
    w: Vector2;
    // ...with optional initializers.
    y : i32 = 42;
    h : Vector2 = v;
    // When initializer is present types may be inferred automagically.
    x1 := x;
    vx := v.x;
    thing := add(vx, r);

    // 2) Assignment statements
    x = 7;
    v = vector2_unit_up();
    // We can assign to procedure arguments, but caller won't see any effect,
    // because all argments are passed by value.
    r = add(a, 24.0);
    v.x = r;
    r = add(v.y, 42.0);
    v.y = add(v.x, r);

    // 3) Return statement, with explicit return value. Even void must be specified.
    ret v;

    // You can just put your tiny local helper procedures after the last return,
    // if your naming is good enough for this to be readable =)
    proc add(a: float32, b: float32) -> float32 { ret a + b; } 
    proc vector2_unit_up() -> Vector2 { v: Vector2; v.y = 1.0; ret v; }
}

// You can make procedures linkable from outside as a DLL/dylib/so symbols
// by using * (star) after 'proc' keyword.
proc* main_i32_i32_reti32(a: i32, b: i32) ->i32 {
    ret add(a * 100, b * 2);
}
